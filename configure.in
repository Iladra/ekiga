AC_INIT(gnomemeeting, 1.3.0, http://bugzilla.gnome.org/enter_bug.cgi?product=gnomemeeting)
AC_CANONICAL_TARGET
AC_PREREQ(2.53)
AC_CONFIG_SRCDIR(src/gnomemeeting.cpp)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)


dnl ###########################################################################
dnl   GnomeMeeting Version
dnl ###########################################################################

MAJOR_VERSION=1
MINOR_VERSION=3
BUILD_TYPE=ReleaseCode
BUILD_NUMBER=0

AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION,[fix])
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION,[fix])
AC_DEFINE_UNQUOTED(BUILD_TYPE, $BUILD_TYPE,[fix])
AC_DEFINE_UNQUOTED(BUILD_NUMBER, $BUILD_NUMBER,[fix])


dnl ###########################################################################
dnl Disable GNOME support
dnl ###########################################################################
AC_ARG_ENABLE(gnome,
        [  --disable-gnome        Remove GNOME support. Don't do this, it removes essential features],
        disable_gnome=yes, disable_gnome=no)

AC_MSG_CHECKING(whether GNOME support must be compiled in)
if test x"${disable_gnome}" = xyes && test x"${enable_eds}" = xno; then
   AC_MSG_RESULT([no])
else
   AC_MSG_RESULT([yes])
fi
AM_CONDITIONAL(DISABLE_GNOME, test x"${disable_gnome}" = xyes)


dnl ###########################################################################
dnl   Gnome-Doc-Utils
dnl ###########################################################################

if test x"${disable_gnome}" = xno ; then
GNOME_DOC_INIT
else
ENABLE_SK_TRUE="#"
ENABLE_SK_FALSE=""
fi

dnl ###########################################################################
dnl   Misc
dnl ###########################################################################

AC_PROG_INTLTOOL([0.20])

AM_PROG_LIBTOOL

AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_HEADER_STDC

AC_CHECK_DECLS(strcasecmp)


dnl ###########################################################################
dnl This is to check correct gconf installation
dnl ###########################################################################
SCHEMA_AGE=57
AC_SUBST(SCHEMA_AGE)


dnl ###########################################################################
dnl GConf related settings
dnl ###########################################################################
if test x"${disable_gnome}" = xyes ; then
AM_CONDITIONAL(GCONF_SCHEMAS_INSTALL, false)
else
AM_GCONF_SOURCE_2
fi


dnl ###########################################################################
dnl PKG_CONFIG and required versions
dnl ###########################################################################
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test x"$PKG_CONFIG" = xno ; then
AC_MSG_ERROR(Couldn't find pkg-config. Please install the pkg-config package)
fi

if test x"${disable_gnome}" = xyes; then
PKG_CHECK_MODULES(GNOMEMEETING, gtk+-2.0 >= 2.4.0 gthread-2.0 >= 2.4.0 libxml-2.0 >= 2.5.0)
GNOMEMEETING_CFLAGS="$GNOMEMEETING_CFLAGS -DDISABLE_GNOME"
else
PKG_CHECK_MODULES(GNOMEMEETING, gtk+-2.0 >= 2.4.0 gthread-2.0 >= 2.4.0 esound >= 0.2.28 gconf-2.0 >= 2.2.0 libxml-2.0 >= 2.6.0 libgnome-2.0 >= 2.2.0 libgnomeui-2.0 >= 2.2.0)
PKG_CHECK_MODULES(EDS, libebook-1.2 >= 1.1.3,ebook12=yes,ebook12=no)
if test x"${ebook12}" = xno ; then
PKG_CHECK_MODULES(EDS, libebook-1.0 >= 0.0.94)
fi
GNOMEMEETING_CFLAGS="$GNOMEMEETING_CFLAGS $EDS_CFLAGS"
GNOMEMEETING_LIBS="$GNOMEMEETING_LIBS $EDS_LIBS"
fi

PWLIB_REC_VERSION="1.9.2"
OPAL_REC_VERSION="2.1.2"


dnl ###########################################################################
dnl Scrollkeeper
dnl ###########################################################################
if test x"${disable_gnome}" = xno ; then
AC_PATH_PROG(SK_CONFIG,scrollkeeper-config,no)
if test x$SK_CONFIG = xno; then
AC_MSG_ERROR(Couldn't find scrollkeeper-config. Please install the scrollkeeper package: http://scrollkeeper.sourceforge.net)
fi
fi


dnl ###########################################################################
dnl Set up Operating System specific parameters and endianess
dnl ###########################################################################

dnl Check the OS type (more types can be added) 
AC_MSG_CHECKING(whether OS type is supported)

case $target_os in

  linux-gnu | linux | Linux)
    ARCH_OPAL_CFLAGS="-DPTRACING -DNDEBUG -Wall -Os -g -fno-exceptions -felide-constructors"
    ARCH_OPAL_LIBS="-lpt -lopal -ldl -lpthread"
    AC_MSG_RESULT([yes])
    gm_platform="linux"

dnl if debug add this and link to -lh323_linux_x86_d -lpt_linux_x86_d
dnl -D_DEBUG -DPMEMORY_CHECK=1 -DPTRACING -g
    ;;

  FreeBSD* | freebsd* )
    ARCH_OPAL_CFLAGS="-Os -Wall -DPTRACING -DNDEBUG -DSTATIC_LIBS_USED"
    ARCH_OPAL_LIBS="-lh323_FreeBSD_x86_r_s -lpt_FreeBSD_x86_r_s -pthread -lssl -lcrypto"
    AC_MSG_RESULT([yes])
    gm_platform="freebsd"
    ;;

  darwin* )
    ARCH_OPAL_CFLAGS="-Os -Wall -DPTRACING -DNDEBUG -DNO_LONG_DOUBLE -DSTATIC_LIBS_USED"
    ARCH_OPAL_LIBS="-multiply_defined suppress -lh323_Darwin_ppc_r_s -lpt_Darwin_ppc_r_s -lssl -lcrypto -lX11 -framework AudioToolbox -framework CoreAudio -framework CoreServices"
    AC_MSG_RESULT([yes])
    gm_platform="macosx"
    ;;

  *)
    AC_MSG_ERROR([Only Linux, FreeBSD and Mac OS X are supported. Please contact the author to know how you can add more OS types.])
    ;;
esac


dnl ###########################################################################
dnl  Check for library path
dnl ###########################################################################
case $host in

*-*-linux*)
  # Test if the compiler is 64bit
  echo 'int i;' > conftest.$ac_ext
  gnomemeeting_cv_cc_64bit_output=no

  if AC_TRY_EVAL(ac_compile); then
    case `/usr/bin/file conftest.$ac_objext` in

    *"ELF 64"*)
    gnomemeeting_cv_cc_64bit_output=yes      
    ;;
    
    esac
  fi

  rm -rf conftest*  
;;
esac

case $host_cpu:$gnomemeeting_cv_cc_64bit_output in
powerpc64:yes | s390x:yes | sparc64:yes | x86_64:yes)
libname="lib64"  
;;
*:*)  
libname="lib"  
;;
esac


dnl ###########################################################################
dnl Check for pthreads library
dnl ###########################################################################
AC_CHECK_LIB(pthread, pthread_create, HAS_PTHREADS=yes, HAS_PTHREADS=no)
if test ${HAS_PTHREADS} != yes ; then
  AC_MSG_ERROR(must have pthreads!)
fi


dnl ###########################################################################
dnl PWLib Headers and Libraries
dnl ###########################################################################
AC_ARG_WITH(pwlib-dir, [  --with-pwlib-dir=PFX   Location of PWLib], with_pwlib_dir="$withval", with_pwlib_dir="/usr")

dnl Check for the includes presence
AC_MSG_CHECKING(for PWLib includes in ${with_pwlib_dir}/include/)
AC_MSG_RESULT()

CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS -I${with_pwlib_dir}/include/ptlib"
AC_CHECK_FILE(${with_pwlib_dir}/include/ptlib/pprocess.h, pwlib_includes="yes", pwlib_includes="no")
if test "x${pwlib_includes}" != "xno" ; then
	PWLIB_CFLAGS="-I${with_pwlib_dir}/include/ptlib -I${with_pwlib_dir}/include/ptclib"
	if test "x${with_pwlib_dir}" != "x/usr"; then
		PWLIB_CFLAGS="${PWLIB_CFLAGS} -I${with_pwlib_dir}/include"
	fi	
fi
CPPFLAGS="$CPPFLAGS_save"

if test "x${pwlib_includes}" = "xno" ; then
	AC_MSG_ERROR(You need the PWLib headers to compile GnomeMeeting)
fi

dnl Checking for the library presence
LIBS_save="$LIBS"
LIBS="${LIBS} -L${with_pwlib_dir}/${libname}/"
AC_CHECK_LIB(pt, main, pwlib_libs="yes", pwlib_libs="no")
LIBS="${LIBS_save}"

if test "x${pwlib_libs}" != "xno"; then
	if test "x${with_pwlib_dir}" != "x/usr"; then
		PWLIB_LIBS="-L${with_pwlib_dir}/${libname}"
	fi
else
	AC_MSG_ERROR(You need the PWLib library to compile GnomeMeeting)
fi

dnl Checking for PWLib version
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $PWLIB_CFLAGS"
AC_MSG_CHECKING(for PWLib version);
AC_EGREP_CPP(${PWLIB_REC_VERSION},
	     [#include <ptbuildopts.h>
	      PWLIB_VERSION 
	     ],AC_MSG_RESULT(${PWLIB_REC_VERSION}), AC_MSG_ERROR(Sorry but the recommended PWLib version is ${PWLIB_REC_VERSION}))
CPPFLAGS="$CPPFLAGS_save"

dnl Checking for OpenLDAP support in PWLIB
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $PWLIB_CFLAGS"
AC_MSG_CHECKING(for OpenLDAP support in PWLIB);
AC_EGREP_CPP(1,
	     [#include <ptbuildopts.h>
	      P_LDAP
	     ],AC_MSG_RESULT(yes), [ AC_MSG_RESULT(no);
	AC_MSG_ERROR(Sorry but the PWLib version you are using doesn't support LDAP)])
CPPFLAGS="$CPPFLAGS_save"

dnl Checking for ENUM support in PWLIB
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $PWLIB_CFLAGS"
AC_MSG_CHECKING(for ENUM support in PWLIB);
AC_EGREP_CPP(1,
	     [#include <ptbuildopts.h>
	      P_DNS
	     ],AC_MSG_RESULT(yes), [ AC_MSG_RESULT(no);
	AC_MSG_ERROR(Sorry but the PWLib version you are using doesn't support ENUM)])
CPPFLAGS="$CPPFLAGS_save"

dnl Check for ptlib-config
AC_CHECK_FILE(${with_pwlib_dir}/bin/ptlib-config, HAS_PTLIB_CONFIG=1)
if test "x${HAS_PTLIB_CONFIG}" != "x1" ; then
        AC_MSG_ERROR([Sorry but ptlib-config can not be found, please check your installation]);
else
        AC_PATH_PROG(PTLIB_CONFIG, ptlib-config, , ${with_pwlib_dir}/bin/)
fi
ARCH_OPAL_CFLAGS="$ARCH_OPAL_CFLAGS `$PTLIB_CONFIG --ccflags`"
                  

dnl ###########################################################################
dnl Opal Headers and Libraries
dnl ###########################################################################
AC_ARG_WITH(opal-dir, [  --with-opal-dir=PFX   Location of Opal], with_opal_dir="$withval", with_opal_dir="/usr")

dnl Check for the includes presence
AC_MSG_CHECKING(for Opal includes in ${with_opal_dir}/include/)
AC_MSG_RESULT()

CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS -I${with_opal_dir}/include/opal"
AC_CHECK_FILE(${with_opal_dir}/include/opal/opal/call.h, opal_includes="yes", opal_includes="no")
if test "x${opal_includes}" != "xno" ; then
	OPAL_CFLAGS="-I${with_opal_dir}/include/opal"
fi
CPPFLAGS="$CPPFLAGS_save"

if test "x${opal_includes}" = "xno" ; then
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS -I${with_opal_dir}/include"
AC_CHECK_FILE(${with_opal_dir}/include/opal/call.h, opal_includes="yes", opal_includes="no")
if test "x${opal_includes}" != "xno" ; then
	OPAL_CFLAGS="-I${with_opal_dir}/include"
fi
CPPFLAGS="$CPPFLAGS_save"
fi

if test "x${opal_includes}" = "xno" ; then
	AC_MSG_ERROR(You need the Opal headers to compile GnomeMeeting)
fi

dnl Checking for the library presence
LIBS_save="$LIBS"
LIBS="${LIBS} -L${with_opal_dir}/${libname}/ -L${with_pwlib_dir}/${libname}/ -lpt"
AC_CHECK_LIB(opal, main, opal_libs="yes", opal_libs="yes")
LIBS="${LIBS_save}"

if test "x${opal_libs}" != "xno"; then
	if test "x${with_opal_dir}" != "x/usr"; then
		OPAL_LIBS="-L${with_opal_dir}/lib"
	fi
else
	AC_MSG_ERROR(You need the Opal library to compile GnomeMeeting)
fi

dnl Checking for Opal version
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $OPAL_CFLAGS"
AC_MSG_CHECKING(for Opal version);
AC_EGREP_CPP(${OPAL_REC_VERSION},
	     [#include <opal/buildopts.h>
	      OPAL_VERSION 
	     ],AC_MSG_RESULT(${OPAL_REC_VERSION}), AC_MSG_ERROR(Sorry but the recommended OPAL version is ${OPAL_REC_VERSION}))
CPPFLAGS="$CPPFLAGS_save"

dnl Checking for IXJ support in Opal
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $OPAL_CFLAGS"
AC_MSG_CHECKING(for IXJ support in OPAL);
AC_EGREP_CPP(1,
	     [#include <opal/opal/buildopts.h>
	      HAS_IXJ
	     ],AC_MSG_RESULT(yes), [ AC_MSG_RESULT(no);
	AC_MSG_ERROR(Sorry but the OPAL version you are using doesn't support IXJ)])
CPPFLAGS="$CPPFLAGS_save"

dnl ###########################################################################
dnl   Check for SDL and disable-sdl
dnl ###########################################################################

AC_ARG_ENABLE(sdl,
        [  --disable-sdl        Remove SDL support.],
        disable_sdl=yes, disable_sdl=no)

AC_MSG_CHECKING(whether SDL support must be compiled in)
if test x"${disable_sdl}" = xyes ; then
   AC_MSG_RESULT([no])
   HAS_SDL="disabled"
else
   AC_MSG_RESULT([yes])
fi

if test x"${disable_sdl}" = xno ; then
dnl SDL does not work on Mac OS X yet, so disable it
if test ${gm_platform} = "macosx" ; then
	HAS_SDL="disabled"
        AC_MSG_WARN([*** SDL cannot be used on Mac OS X. Fullscreen mode will be disabled])

else

	SDL_VERSION=1.2.4
	SDL_HAS_SDL=
	AM_PATH_SDL($SDL_VERSION,
	            SDL_HAS_SDL="-DHAS_SDL",
		    AC_MSG_WARN([*** SDL version $SDL_VERSION not found!. Fullscreen mode will be disabled]))
	SDL_CFLAGS="$SDL_CFLAGS $SDL_HAS_SDL" 
	SDL_LDFLAGS="$LIBS $SDL_LIBS"

	if test "x${SDL_HAS_SDL}" = "x-DHAS_SDL" ; then
		HAS_SDL="enabled"
	else  
		HAS_SDL="disabled"
	fi
fi
fi


dnl ###########################################################################
dnl LDAP Headers and Libraries
dnl ###########################################################################
AC_ARG_WITH(ldap-dir, [  --with-ldap-dir=PFX   Location of LDAP], with_ldap_dir="$withval", with_ldap_dir="/usr")

dnl Check for the includes presence
AC_MSG_CHECKING(for LDAP includes in ${with_ldap_dir}/include/)
AC_MSG_RESULT()

CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS -I${with_ldap_dir}/include"
AC_CHECK_FILE(${with_ldap_dir}/include/ldap.h, ldap_includes="yes", ldap_includes="no")
CPPFLAGS="$CPPFLAGS_save"
	
if test "x${ldap_includes}" != "xno" ; then
	LDAP_CFLAGS="-I${with_ldap_dir}/include"
else
	AC_MSG_ERROR(You need the LDAP headers to compile GnomeMeeting)
fi

dnl Checking for the library presence
LIBS_save="$LIBS"
LIBS="${LIBS} -L${with_ldap_dir}/${libname}/ -llber"
AC_CHECK_LIB(ldap, main, ldap_libs="yes", ldap_libs="no")
LIBS="${LIBS_save}"

if test "x${ldap_libs}" != "xno"; then
	LDAP_LIBS="-llber -lldap"
	if test "x${with_ldap_dir}" != "x/usr"; then
		LDAP_LIBS="-L${with_ldap_dir}/${libname} $LDAP_LIBS"
	fi
else
	AC_MSG_ERROR(You need the LDAP library to compile GnomeMeeting)
fi

dnl Checking for libresolv
if test ${gm_platform} = "linux" ; then
	AC_CHECK_LIB(resolv, res_gethostbyaddr, [LIBS="-lresolv $LIBS"], AC_MSG_ERROR([*** libresolv not found.]), -lresolv)
	LDAP_LIBS="${LDAP_LIBS} -lresolv"
fi


dnl #########################################################################  
dnl  Check for recent libxml2 which has xmlRegisterNodeDefault()
dnl ########################################################################   
AC_CHECK_LIB(xml2, xmlFreeDoc)
AC_CHECK_FUNCS(xmlRegisterNodeDefault)


dnl ########################################################################   
dnl  libxml path is in GNOMEMEETING_CFLAGS due to pkg-config
dnl ########################################################################   
CPPFLAGS_save="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $GNOMEMEETING_CFLAGS"
AC_CHECK_TYPES(xmlSAXHandlerV1,,, [#include <libxml/SAX.h>])
CPPFLAGS="$CPPFLAGS_save"


dnl #########################################################################
dnl   Check for DBUS
dnl #########################################################################
AC_ARG_ENABLE(dbus,
	[  --enable-dbus       Enable the DBUS component.],
	enable_dbus=yes, enable_dbus=no)

AC_MSG_CHECKING(whether the DBUS component should be compiled in)
if test x"${enable_dbus}" = xyes ; then
   DBUS="enabled"
   AC_MSG_RESULT([yes])
else
   DBUS="disabled"
   AC_MSG_RESULT([no])
fi

if test x"${enable_dbus}" = xyes ; then
   PKG_CHECK_MODULES(DBUS,  dbus-1 = 0.22 dbus-glib-1 = 0.22)
   GNOMEMEETING_CFLAGS="$GNOMEMEETING_CFLAGS $DBUS_CFLAGS -DHAS_DBUS"
   GNOMEMEETING_LIBS="$GNOMEMEETING_LIBS $DBUS_LIBS"
fi
AM_CONDITIONAL(HAS_DBUS, test x"${enable_dbus}" = xyes)
AM_CONDITIONAL(DBUS_SERVICES_INSTALL, test x"${enable_dbus}" = xyes)


dnl #########################################################################
dnl   Check for Howl
dnl #########################################################################
AC_ARG_ENABLE(howl,
       [  --disable-howl       Disable the HOWL component.],
       disable_howl=yes, disable_howl=no)

AC_MSG_CHECKING(whether the HOWL component should be compiled in)
if test x"${disable_howl}" = xyes ; then
   HOWL="disabled"
   AC_MSG_RESULT([no])
else
   HOWL="enabled"
   AC_MSG_RESULT([yes])
fi

if test x"${disable_howl}" = xno ; then
   PKG_CHECK_MODULES(HOWL,  howl >= 0.9.7)
   GNOMEMEETING_CFLAGS="$GNOMEMEETING_CFLAGS $HOWL_CFLAGS -DHAS_HOWL"
   GNOMEMEETING_LIBS="$GNOMEMEETING_LIBS $HOWL_LIBS"
fi
AM_CONDITIONAL(HAS_HOWL, test x"${disable_howl}" = xno)


dnl ###########################################################################
dnl  The various CFLAGS are merged into GNOMEMEETING_CFLAGS and 
dnl  GNOMEMEETING_LIB_CFLAGS
dnl ###########################################################################
GNOMEMEETING_LIB_CFLAGS="$GNOMEMEETING_CFLAGS"
GNOMEMEETING_CFLAGS="$GNOMEMEETING_CFLAGS $PWLIB_CFLAGS $LDAP_INCLUDES $OPAL_CFLAGS $SDL_CFLAGS $LDAP_CFLAGS $ARCH_OPAL_CFLAGS"
GNOMEMEETING_LIBS="$GNOMEMEETING_LIBS $PWLIB_LIBS $OPAL_LIBS $LDAP_LIBS $SDL_LDFLAGS $ARCH_OPAL_LIBS"
CFLAGS=""


dnl ###########################################################################
dnl For the static library
dnl ###########################################################################
AC_SUBST(GNOMEMEETING_LIB_CFLAGS)


dnl #########################################################################
dnl  Support for internationalization
dnl ########################################################################
ALL_LINGUAS="am ar az be bg bn bs ca cs cy da de el en_CA en_GB es eu fi fr ga gl gu hi hr hu is it ja ko lt lv mk ml mn mr ms nb nl no pa pl pt pt_BR ro ru sk sq sr sr@Latn sv ta tr uk vi wa zh_CN zh_TW"

GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",[fix])
AM_GLIB_GNU_GETTEXT


AC_SUBST(GNOMEMEETING_LDADD)
AC_SUBST(GNOMEMEETING_CFLAGS)


dnl #########################################################################
dnl Set PACKAGE_LOCALE_DIR in config.h.
dnl #########################################################################
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale",[fix])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale",[fix])
fi


dnl ###########################################################################
dnl  Data and configuration directories for the system
dnl ###########################################################################
gnomedatadir=`eval "echo ${datadir}"`
gnomeconfdir=`eval "echo ${sysconfdir}"`
                                                                                AC_SUBST(gnomedatadir)
AC_SUBST(gnomeconfdir)
                                                                                        
dnl ###########################################################################
dnl  Output the different Makefiles
dnl ###########################################################################
AC_OUTPUT(
Makefile
src/gnomemeeting-config-tool
AC_PACKAGE_NAME.schemas.in
AC_PACKAGE_NAME.service
AC_PACKAGE_NAME.desktop.in
lib/Makefile
lib/about/Makefile
lib/druid/Makefile
lib/widgets/Makefile
lib/xdap/Makefile
lib/contacts/Makefile
po/Makefile.in
man/Makefile
src/Makefile
pixmaps/Makefile
sounds/Makefile
xdap_data/Makefile
help/Makefile)

dnl ###########################################################################
dnl Summary
dnl ###########################################################################

echo ""
echo "================ Final configuration ==================="
echo "          Installing into prefix  :  $prefix"
echo ""
echo "                 OPAL Version is  :  $OPAL_REC_VERSION"
echo "               OPAL Directory is  :  $with_opal_dir"
echo "                PWLIB Version is  :  $PWLIB_REC_VERSION"
echo "              PWLIB Directory is  :  $with_pwlib_dir"
echo "                 ptlib-config is  :  $with_pwlib_dir/bin/ptlib-config"
echo ""
echo "                Quicknet support  :  $HAS_IXJ"
echo "          SDL Fullscreen support  :  $HAS_SDL"
echo "                    DBUS support  :  $DBUS"
echo "                Zeroconf support  :  $HOWL"
echo ""
echo "                         OS Type  :  $target_os"
echo "                    Machine Type  :  $target_cpu"
echo ""
echo " If all settings are OK, type make and make install "
echo "========================================================"
