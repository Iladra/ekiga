AC_INIT(gnomemeeting, 0.94.0, http://bugzilla.gnome.org/enter_bug.cgi?product=gnomemeeting)
AC_CANONICAL_TARGET
AC_PREREQ(2.50)
AC_CONFIG_SRCDIR(src/gnomemeeting.cpp)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)


AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_PROG_INTLTOOL([0.20])

AM_PROG_LIBTOOL

AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
AC_HEADER_STDC


dnl ###########################################################################
dnl   Versioning
dnl ###########################################################################

MAJOR_VERSION=0
MINOR_VERSION=94
BUILD_TYPE=ReleaseCode
BUILD_NUMBER=0

AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION,[fix])
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION,[fix])
AC_DEFINE_UNQUOTED(BUILD_TYPE, $BUILD_TYPE,[fix])
AC_DEFINE_UNQUOTED(BUILD_NUMBER, $BUILD_NUMBER,[fix])


dnl ###########################################################################
dnl   This is to check correct gconf installation
dnl ###########################################################################
SCHEMA_AGE=10
dnl increment this number if you add anything to the schema file
AC_SUBST(SCHEMA_AGE)


dnl ###########################################################################
dnl   PKG_CONFIG
dnl ###########################################################################
PKG_CHECK_MODULES(GNOMEMEETING, libgnomeui-2.0 >= 1.112.0 linc >= 0.5.0 libbonobo-2.0 >= 2.0.0 esound >= 0.2.28)


dnl ###########################################################################
dnl For the static library
dnl ###########################################################################
AC_SUBST(GNOMEMEETING_CFLAGS)


dnl ###########################################################################
dnl GConf related settings
dnl ###########################################################################
AM_GCONF_SOURCE_2


dnl ###########################################################################
dnl Set up Operating System specific parameters and endianess
dnl ###########################################################################
ARCH_H323_CFLAGS=""

dnl Check if we are on 64 bits architectures
AC_CHECK_SIZEOF(long)
if test x$ac_cv_sizeof_long = x8; then
   ARCH_H323_CFLAGS="$ARCH_H323_CFLAGS -DP_64BIT"
fi

dnl Check if we are on Big Endian or Little Endian
AC_C_BIGENDIAN
case xyes in
x$ac_cv_c_bigendian)
   ARCH_H323_CFLAGS="$ARCH_H323_CFLAGS -DPBYTE_ORDER=PBIG_ENDIAN"
  ;;
*)
   ARCH_H323_CFLAGS="$ARCH_H323_CFLAGS -DPBYTE_ORDER=PLITTLE_ENDIAN"
  ;;
esac
	   
dnl Check the OS type (more types can be added) 
AC_MSG_CHECKING(whether OS type is supported)

case $target_os in

  linux-gnu | linux | Linux)
    H323_CFLAGS="-DP_LINUX -D_REENTRANT -DP_HAS_SEMAPHORES -DP_PTHREADS $ARCH_H323_CFLAGS -DHAS_OSS -O3 -DNDEBUG -fPIC -g -DPHAS_TEMPLATES -Wall"
    H323_LIBS="-lopenh323 -ldl -lpt -lpthread -lssl -lcrypto"
    LIBPT_FILE="libpt.so"
    LIBOPENH323_FILE="libopenh323.so"
    AC_MSG_RESULT([yes])

dnl if debug add this and link to -lh323_linux_x86_d -lpt_linux_x86_d
dnl -D_DEBUG -DPMEMORY_CHECK=1 -DPTRACING -g
    ;;

  FreeBSD)
    H323_CFLAGS="-DP_FREEBSD=400001 -DP_SSL -DP_PTHREADS  $ARCH_H323_CFLAGS -O2 -Wall -DPHAS_TEMPLATES"
    H323_LIBS="-lh323_FreeBSD_x86_r -lpt_FreeBSD_x86_r -pthread -lssl -lcrypto"
    LIBPT_FILE="libpt_FreeBSD_x86_r_s.a"
    LIBOPENH323_FILE="libh323_FreeBSD_x86_r_s.a"
    AC_MSG_RESULT([yes])
    ;;
  *)
    AC_MSG_ERROR("Only Linux and FreeBSD are supported. Please contact the author. You can add more OS types.")
    ;;
esac


dnl ###########################################################################
dnl PTLib Headers and Libraries
dnl ###########################################################################
AC_ARG_WITH(ptlib-includes, [  --with-ptlib-includes=PFX   Location of PTLib includes.], with_ptlib_includes="$withval", with_ptlib_includes="/usr/include/ptlib")
have_ptlib_includes="no"

if test "x${with_ptlib_includes}" != "xno"; then
	CPPFLAGS_save="$CPPFLAGS"
	
	AC_MSG_CHECKING(for PTLib includes)
	AC_MSG_RESULT()
	
	CPPFLAGS="$CPPFLAGS -I${with_ptlib_includes}/unix -I${with_ptlib_includes}"
	ptlib_includes="yes"
	CPPFLAGS="$CPPFLAGS_save"
	
	if test "x${ptlib_includes}" != "xno" -a "x${ptlib_includes}" != "x"; then
		have_ptlib_includes="yes"
		PTLIB_CFLAGS="-I${with_ptlib_includes}/unix -I${with_ptlib_includes}/.. -I${with_ptlib_includes}/../ptclib"
		PTLIB_INCLUDE_DIR="${with_ptlib_includes}"
	else
		AC_MSG_ERROR([You need PTLib's header files to build GnomeMeeting])
	fi
else
	AC_MSG_ERROR([You need PTLib's header files to build GnomeMeeting])
fi


AC_ARG_WITH(ptlib-libs, [  --with-ptlib-libs=PFX       Location of PTLib libs.], with_ptlib_libs="$withval", with_ptlib_libs="/usr/lib")
if test "x${with_ptlib_libs}" != "xno" -a "x${have_ptlib_includes}" != "xno"; then
	AC_CHECK_FILE(${with_ptlib_libs}/${LIBPT_FILE}, ptlib_libs="yes", ptlib_libs="no")
	
	if test "x${ptlib_libs}" != "xno"; then
		PTLIB_LDFLAGS="-L${with_ptlib_libs}"
		PTLIB_LIBS_DIR="${with_ptlib_libs}"
	else
		AC_MSG_ERROR([You need PTLib to enjoy GnomeMeeting])
	fi
else
	AC_MSG_ERROR([You need PTLib to enjoy GnomeMeeting])
fi


dnl ###########################################################################
dnl OpenH323
dnl ###########################################################################
AC_ARG_WITH(openh323-includes, [  --with-openh323-includes=PFX   Location of OpenH323 includes.], with_openh_includes="$withval", with_openh_includes="/usr/include/openh323")
have_openh323_includes="no"
if test "x${with_openh_includes}" != "xno" -a "x${have_ptlib_includes}" != "xno"; then
	CPPFLAGS_save="$CPPFLAGS"
	
	AC_MSG_CHECKING(for OpenH323 includes)
	AC_MSG_RESULT()
	
	CPPFLAGS="$CPPFLAGS -I$PTLIB_CFLAGS -I${with_openh_includes}"
	openh323_includes="yes"
	CPPFLAGS="$CPPFLAGS_save"
	
	if test "x${openh323_includes}" != "xno" -a "x${openh323_includes}" != "x"; then
		have_openh323_includes="yes"
		OPENH323_CFLAGS="-I${with_openh_includes}"
		OPENH323_INCLUDE_DIR="${with_openh_includes}"
	else
		AC_MSG_ERROR([You need OpenH323's header files to build GnomeMeeting])
	fi
else
	AC_MSG_ERROR([You need OpenH323's header files to build GnomeMeeting])
fi


AC_ARG_WITH(openh323-libs, [  --with-openh323-libs=PFX       Location of OpenH323 libs.], with_openh323_libs="$withval", with_openh323_libs="/usr/lib")
msg_openh323="no"

if test "x${with_openh323_libs}" != "xno" -a "x${have_openh323_includes}" != "xno"; then
	AC_CHECK_FILE(${with_openh323_libs}/${LIBOPENH323_FILE}, openh323_libs="yes", openh323_libs="no")
	if test "x${openh323_libs}" != "xno"; then
		msg_openh323="yes"
		OPENH323_LDFLAGS="-L${with_openh323_libs}"
		OPENH323_LIBS_DIR="${with_openh323_libs}"
	else
		AC_MSG_ERROR([You need OpenH323 to enjoy GnomeMeeting])
	fi
else
	AC_MSG_ERROR([You need OpenH323 to enjoy GnomeMeeting])
fi


dnl ###########################################################################
dnl Check if there is IXJ support
dnl ###########################################################################
CPPFLAGS_save="$CPPFLAGS"
	
CPPFLAGS="$CPPFLAGS -I /usr/include/linux -I /usr/include/sys -I /usr/local/include/sys"
AC_CHECK_HEADER(telephony.h, HAS_IXJ="enabled", HAS_IXJ="disabled",-)

if test "x${HAS_IXJ}" == "xenabled"; then
	H323_CFLAGS="$H323_CFLAGS -DHAS_IXJ"
fi

CPPFLAGS="$CPPFLAGS_save"


dnl ###########################################################################
dnl Check if there is Speex codec support
dnl ###########################################################################
CPPFLAGS_save="$CPPFLAGS"
	
CPPFLAGS="$CPPFLAGS -I /usr/include/ -I /usr/local/include/"
AC_CHECK_HEADER(speex.h, HAS_SPEEX="enabled", HAS_SPEEX="disabled",-)

if test "x${HAS_SPEEX}" == "xenabled"; then
	H323_CFLAGS="$H323_CFLAGS -DSPEEX_CODEC"
	H323_LIBS="$H323_LIBS -lspeex"	
fi

CPPFLAGS="$CPPFLAGS_save"


dnl ###########################################################################
dnl   Check for SDL
dnl ###########################################################################
SDL_VERSION=1.1.3
SDL_HAS_SDL=
AM_PATH_SDL($SDL_VERSION,
            SDL_HAS_SDL="-DHAS_SDL",
	    AC_MSG_WARN([*** SDL version $SDL_VERSION not found!. Fullscreen mode will be disabled]))
SDL_CFLAGS="$SDL_CFLAGS $SDL_HAS_SDL" 
SDL_LDFLAGS="$LIBS $SDL_LIBS"

if test "x${SDL_HAS_SDL}" == "x-DHAS_SDL" ; then
	HAS_SDL="enabled"
else  
	HAS_SDL="disabled"
fi


dnl ###########################################################################
dnl OpenSSL
dnl ###########################################################################
AC_ARG_WITH(openssl-includes, [  --with-openssl-includes=PFX   Location of OpenSSL includes.], with_openssl_includes="$withval", with_openssl_includes="/usr/include/openssl")
have_openssl_includes="no"

if test "x${with_openssl_includes}" != "xno" ; then
        CPPFLAGS_save="$CPPFLAGS"

        AC_MSG_CHECKING(for OpenSSL includes)
        AC_MSG_RESULT()

        CPPFLAGS="$CPPFLAGS -I$PTLIB_CFLAGS -I${with_openssl_includes}"
        AC_CHECK_HEADER(ssl.h, [ openssl_includes="yes" ])
        CPPFLAGS="$CPPFLAGS_save"

        if test "x${openssl_includes}" != "xno" -a "x${openssl_includes}" != "x"; then
                have_openssl_includes="yes"
                OPENSSL_CFLAGS="-I${with_openssl_includes}"
                OPENSSL_INCLUDE_DIR="${with_openssl_includes}"
        else
                AC_MSG_ERROR([You need OpenSSL's header files to build GnomeMeeting])
        fi
else
        AC_MSG_ERROR([You need OpenSSL's header files to build GnomeMeeting])
fi


AC_ARG_WITH(openssl-libs, [  --with-openssl-libs=PFX       Location of OpenSSL libs.], with_openssl_libs="$withval", with_openssl_libs="/usr/lib")
msg_openssl="no"

if test "x${with_openssl_libs}" != "xno" -a "x${have_openssl_includes}" != "xno"; then
        AC_CHECK_FILE(${with_openssl_libs}/libssl.so, openssl_libs="yes", openssl_libs="no")
        if test "x${openssl_libs}" != "xno"; then
                msg_openssl="yes"
                OPENSSL_LDFLAGS="-L${with_openssl_libs} ${SSL_LIBS}"
                OPENSSL_LIBS_DIR="${with_openssl_libs}"
        else
                AC_MSG_ERROR([You need OpenSSL to enjoy GnomeMeeting])
        fi
else
        AC_MSG_ERROR([You need OpenSSL to enjoy GnomeMeeting])
fi


dnl ###########################################################################
dnl LDAP Checks
dnl ###########################################################################

AC_ARG_WITH(openldap-includes, [  --with-ldap-includes=PFX   Location of OpenLDAP includes.],
        with_openldap_includes="$withval", with_openldap_includes="/usr/include")
have_openldap_includes="no"
if test "x${with_openldap_includes}" != "xno" ; then
        CPPFLAGS_save="$CPPFLAGS"

        AC_MSG_CHECKING(for OpenLDAP includes)
        AC_MSG_RESULT()

        CPPFLAGS="$CPPFLAGS -I${with_openldap_includes}"
        AC_CHECK_HEADER(ldap.h, [ openldap_includes="yes" ])
        CPPFLAGS="$CPPFLAGS_save"

        if test "x${openldap_includes}" != "xno" -a "x${openldap_includes}" != "x"; then
                have_openldap_includes="yes"
                LDAP_INCLUDES="-I${with_openldap_includes}"
        else
                AC_MSG_ERROR([You need OpenLDAP's header files to build GnomeMeeting])
        fi
else
        AC_MSG_ERROR([You need OpenLDAP's header files to build GnomeMeeting])
fi

AC_ARG_WITH(openldap-libs, [  --with-openldap-libs=PFX       Location of OpenLDAP libs.],
        with_openldap_libs="$withval", with_openldap_libs="/usr/lib")
msg_openldap="no"
if test "x${with_openldap_libs}" != "xno" -a "x${have_openldap_includes}" != "xno"; then
        AC_CHECK_FILE(${with_openldap_libs}/libldap.so, openldap_libs="yes", openldap_libs="no")
        if test "x${openldap_libs}" != "xno"; then
                msg_openldap="yes"
		LDAP_LIBS="-L${with_openldap_libs} -lldap -llber -lresolv ${LDAP_LIBS}"
        else
                AC_MSG_ERROR([You need OpenLDAP to enjoy GnomeMeeting])
        fi
else
        AC_MSG_ERROR([You need OpenLDAP to enjoy GnomeMeeting])
fi


GNOMEMEETING_INCLUDES="$GNOMEMEETING_CFLAGS $PTLIB_CFLAGS $LDAP_INCLUDES $OPENH323_CFLAGS $OPENSSL_CFLAGS $H323_CFLAGS $SDL_CFLAGS $ARTSC_CFLAGS"
GNOMEMEETING_LDADD="$PTLIB_LDFLAGS $OPENH323_LDFLAGS $OPENSSL_LDFLAGS $GNOMEMEETING_LIBS $LDAP_LIBS $SDL_LDFLAGS $ARTSC_LDFLAGS $H323_LIBS"
CFLAGS=""

CXXFLAGS="-O3 $ORBIT_CFLAGS"


dnl #########################################################################
dnl  Check for LDAP if available.
dnl ########################################################################
AC_CHECK_LIB(resolv, res_gethostbyaddr, [LIBS="-lresolv $LIBS"], AC_MSG_ERROR([*** libresolv not found.]), -lresolv)

LIBS="$LDAP_LIBS $LIBS"

AC_CHECK_LIB(ldap, ldap_simple_bind, LIBS="$LDAP_LIBS $LIBS", AC_MSG_ERROR([*** LDAP library is not found.]), -llber)


dnl #########################################################################
dnl  Support for internationalization
dnl ########################################################################
ALL_LINGUAS="cs da de el es fr gl hu it ja nl no pl pt pt_BR ru sk sv ta tr vi wa zh_CN zh_TW"

GETTEXT_PACKAGE=gnomemeeting
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",[fix])
AM_GLIB_GNU_GETTEXT


AC_SUBST(GNOMEMEETING_LDADD)
AC_SUBST(GNOMEMEETING_INCLUDES)


dnl #########################################################################
dnl Set PACKAGE_LOCALE_DIR in config.h.
dnl #########################################################################
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale",[fix])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale",[fix])
fi

AC_MSG_CHECKING([whether to install into Gnome's prefix])
if test x"$use_system_install" = xyes ; then
	AC_MSG_RESULT([yes])
	gnomedatadir=`${GNOME_CONFIG} --datadir`
	gnomeconfdir=`${GNOME_CONFIG} --sysconfdir`
else
	AC_MSG_RESULT([no])
	gnomedatadir=`eval "echo ${datadir}"`
	gnomeconfdir=`eval "echo ${sysconfdir}"`
fi

AC_SUBST(gnomedatadir)
AC_SUBST(gnomeconfdir)

AC_ARG_ENABLE(install_schemas,
        [  --disable-install-schemas        Disable installation of the gconf schemas])
AM_CONDITIONAL(INSTALL_SCHEMAS, test x$enable_install_schemas != xno)
	
AC_OUTPUT(
Makefile
src/gnomemeeting-config-tool
gnomemeeting.schemas.in
lib/Makefile
lib/widgets/Makefile
lib/xdap/Makefile
po/Makefile.in
man/Makefile
src/Makefile
pixmaps/Makefile
sounds/Makefile
xdap_data/Makefile)


dnl ###########################################################################
dnl Summary
dnl ###########################################################################

echo ""
echo "================ Final configuration ==================="
echo "    Installing into prefix: $prefix"
echo ""
echo "    OpenSSL Include Directory is  :  $OPENSSL_INCLUDE_DIR"
echo "  OpenSSL Libraries Directory is  :  $OPENSSL_LIBS_DIR"
echo "   OpenH323 Include Directory is  :  $OPENH323_INCLUDE_DIR"
echo " OpenH323 Libraries Directory is  :  $OPENH323_LIBS_DIR"
echo "      PTLIB Include Directory is  :  $PTLIB_INCLUDE_DIR"
echo "    PTLIB Libraries Directory is  :  $PTLIB_LIBS_DIR"
echo "               LDAP Directory is  :  $LDAP_DIR"
echo ""
echo "                Quicknet support  :  $HAS_IXJ"
echo "          SDL Fullscreen support  :  $HAS_SDL"
echo "           Speex GPL audio codec  :  $HAS_SPEEX"
echo ""
echo "                         OS Type  :  $target_os"
echo "                    Machine Type  :  $target_cpu"
echo ""
echo " If all settings are OK, type make and make install "
echo "========================================================"
